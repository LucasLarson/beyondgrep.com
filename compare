#!/usr/bin/env perl

use strict;
use warnings;
use 5.010;

use JSON::PP;
use File::Slurp;
use List::MoreUtils qw( uniq );
use Template;

# Active columns defined in template
my @TOOLS = qw( ack ag git gnu rg  );
my %TOOLS = map { $_ => 1 } @TOOLS;

# Additional tool values collected, valid, but to be ignored ... 
# TODO: ... until refactor to render Grep-variant-suffixes in grep column
my @GrepVariants = qw( bsd freebsd netbsd osx posix ); 
@TOOLS{@GrepVariants} = ((0 ) x scalar @GrepVariants);
# {use Data::Dumper; local $Data::Dumper::Sortkeys=1; local $Data::Dumper::Trailingcomma=1; warn Dumper( %TOOLS  )}

my $json_text = File::Slurp::read_file( 'comparison.json' );

my $sections = JSON::PP->new->utf8->decode($json_text);

for my $section ( @{$sections} ) {
    my $heading = $section->{heading};
    my $abilities = $section->{abilities};

    for my $ability ( @{$abilities} ) {
        my %flags_by_tool;
        while ( my ($key,$value) = each %{$ability->{how}} ) {
            for my $tool ( split( /\s*,\s*/, $key ) ) {
                defined($TOOLS{$tool}) or die "Invalid tool '$tool' in key '$key'";
                next unless $TOOLS{$tool}; ## TODO Need to accumulate Grep variants with footnotes here
                $flags_by_tool{$tool} = $value;
            }
        }

        my @distinct_flags = uniq values %flags_by_tool;

        # If there's only one how, then the template knows it applies to all.
        my @new_hows;
        if ( @distinct_flags == 1 && keys %flags_by_tool == 5 ) {
            @new_hows = @distinct_flags;
        }
        else {
            @new_hows = map { $flags_by_tool{$_} // '' } @TOOLS;
        }

        s/-/\N{NON-BREAKING HYPHEN}/g for @new_hows;

        $ability->{how} = \@new_hows;
    }
}

# {use Data::Dumper; local $Data::Dumper::Sortkeys=1; local $Data::Dumper::Trailingcomma=1; warn Dumper( $sections )}


my $vars = {
    sections => $sections,
};

my %tt_settings = (
    INCLUDE_PATH => [ qw( . tt tt/wrapper ) ],
);

my $tt = Template->new( \%tt_settings );
$tt->process( 'chart.ttml', $vars ) or die $tt->error;
